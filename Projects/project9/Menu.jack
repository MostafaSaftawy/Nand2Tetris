/** Class for displaying menu and storing settings */
class Menu {
    field int value;

    /** New menu with default settings */
    constructor Menu new() {
        let value = 1;
        return this;
    }

    /** Destroy Game and deallocate memory */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Invoke reaction to a pressed key
     *
     *    | phys. key | key code | response | meaning     |
     *    | --------- | -------- | -------- | ----------- |
     *    |     1     |    49    |    1     | New game    |
     *    |     2     |    50    |    2     | Resume game |
     *    |     e     |    69    |   99     | Exit game   |
     *    |     t     |    84    |   42     | Run tests   |
     *    |   else    |    --    |    0     | do nothing  |
     *
     */
    method int pressedKey(int key) {
        var int response;
        let response = 0;

        if (key = 49) {           // 1
            let response = 1;
        } else { if (key = 50) {  // 2
            let response = 2;
        } else { if (key = 69) {  // e
            let response = 99;
        } else { if (key = 84) {  // t
            let response = 42;
        }}}}

        return response;
    }

    /** Clear the screen and show the menu */
    method void show(boolean canResume) {
        do Screen.clearScreen();
        do Output.moveCursor(0, 0);
        do Output.printString("The Guy Game");
        do Output.println();
        do Output.printString("1) New game");
        if (canResume) {
            do Output.println();
            do Output.printString("2) Resume");
        }
        do Output.println();
        do Output.printString("e) Exit");
        do Output.println();
        do Output.println();
        do Output.printString("t) Run tests");

        return;
    }
}